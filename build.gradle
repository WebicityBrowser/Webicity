import org.gradle.internal.os.OperatingSystem

plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'application'
}

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		project.ext.skijaNatives = "skija-linux-x64"
		project.ext.skijaVersion = "0.116.1"
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		project.ext.skijaNatives = "skija-windows-x64"
		project.ext.skijaVersion = "0.116.1"
		break
	case OperatingSystem.MAC_OS:
		applicationDefaultJvmArgs = ["-XstartOnFirstThread"]
		project.ext.lwjglNatives = "natives-macos-arm64"
		project.ext.skijaNatives = "skija-macos-arm64"
		project.ext.skijaVersion = "0.116.1"
		break
}

repositories {
    mavenCentral()
}

dependencies {
	implementation platform("org.lwjgl:lwjgl-bom:3.3.2")

	implementation "org.lwjgl:lwjgl"
	implementation "org.lwjgl:lwjgl-glfw"
	implementation "org.lwjgl:lwjgl-opengl"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
	
	implementation 'com.github.marianobarrios:tls-channel:0.8.0'
	
	implementation 'com.github.taucher2003:t2003-logger-impl:1.0.2'
	implementation 'com.github.taucher2003:t2003-logger-binder:1.0.2'
	
	implementation 'com.google.code.gson:gson:2.10.1'
	
	implementation "io.github.humbleui:${skijaNatives}:${skijaVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.5.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

mainClassName = "com.github.webicitybrowser.webicitybrowser.Main"

jar {
	manifest {
		attributes(
			"Main-Class": mainClassName,
			"Multi-Release": "true"
		)
	}
}